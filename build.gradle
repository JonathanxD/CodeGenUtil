buildscript {
    ext.kotlin_version = '1.1.0-rc-91'
    ext.dokka_version = '0.9.10'

    repositories {
        mavenCentral()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group 'com.github.jonathanxd'
version '1.1'

allprojects {
    // IDE
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    // Programming Language
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'java'

    // License
    apply plugin: 'license'

    // Shade
    apply plugin: 'com.github.johnrengelman.shadow'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    // Utilities
    compile 'com.github.JonathanxD:CodeAPI:3.1.0'
    compile 'com.github.JonathanxD:CodeAPI-Conversions:1.2.2'
    compile 'com.github.JonathanxD:JwIUtils:3.5.2'

    testCompile 'com.github.JonathanxD:CodeAPI-BytecodeWriter:3.1.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

shadowJar {
    classifier = 'shaded'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    includes = ['src/main/Module.md']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}


license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'JAVADOC_STYLE'
        kt = 'JAVADOC_STYLE'
    }
}
